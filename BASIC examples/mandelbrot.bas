10 REM *********************************************
20 REM **              MANDELBROT SET             **
30 REM **                                         **
40 REM **            MONOCHROME VERSION           **
50 REM *********************************************
60 REM ORIGINAL CODE BY JOSEPH DEWEY
70 REM LM80C BASIC PORT BY LEONARDO MILIANI
80 REM ** TO CHANGE THE SHAPE, CHANGE XL,XU,YL,YU.**
90 REM ** RE CHANGES THE RESOLUTION**
100 XL=-2.000:XU=0.500
110 YL=-1.100:YU=1.100
120 RE=20:WI=256:HE=192
130 XI=(XU-XL)/WI
140 YI=(YU-YL)/HE
200 REM MAIN ROUTINE
210 SCREEN 2:COLOR 6,1,4
220 FOR J=0 TO HE-1
230 FOR I=0 TO WI-1
235 REM CALCULATE MANDELBROT
240 IS=-1
250 NR=XL+I*XI
260 NI=YL+J*YI
270 RZ=0:IZ=0
280 R2=0:I2=0
290 FOR K=1 TO RE
300 R2=RZ*RZ-IZ*IZ
310 I2=2*RZ*IZ
320 RZ=R2+NR
330 IZ=I2+NI
340 IF (RZ*RZ+IZ*IZ)>4 THEN IS=0:GOTO 380
350 NEXT K
355 REM PLOT MANDELBROT
380 PLOT I,J,6 AND NOT IS
390 NEXT I
400 NEXT J
410 VOLUME 1,15:SOUND 1,4000,10:A=INKEY(10)
420 A=INKEY(10):IF A=0 THEN 420
430 END

----------------------------------------------------------------------------------------

10 REM *********************************************
20 REM **              MANDELBROT SET             **
30 REM **                                         **
40 REM **            MULTICOLOR VERSION           **
50 REM *********************************************
60 REM ORIGINAL CODE BY JOSEPH DEWEY
70 REM LM80C BASIC PORT BY LEONARDO MILIANI
80 REM ** TO CHANGE THE SHAPE, CHANGE XL,XU,YL,YU.**
90 REM ** RE CHANGES THE RESOLUTION**
90 REM **TO CHANGE THE SHAPE, CHANGE**
91 REM **XL,XU,YL,YU. RE CHANGES  **
92 REM **THE RESOLUTION**
100 XL=-2.000:XU=0.500
110 YL=-1.100:YU=1.100
120 RE=20:WI=256:HE=192
130 XI=(XU-XL)/WI
140 YI=(YU-YL)/HE
200 REM MAIN ROUTINE
210 SCREEN2:COLOR15,1,5
220 FOR J=0 TO HE-1:OC=-1
230 FOR I=0 TO WI-1
235 REM CALCULATE MANDELBROT
240 C=0
250 NR=XL+I*XI
260 NI=YL+J*YI
270 RZ=0:IZ=0
280 R2=0:I2=0
290 FOR K=1 TO RE
300 R2=RZ*RZ-IZ*IZ
310 I2=2*RZ*IZ
320 RZ=R2+NR
330 IZ=I2+NI
340 IF (RZ*RZ+IZ*IZ)>4 THEN 360
350 NEXT K
355 REM PLOT MANDELBROT
360 IF K<4THENC=6:GOTO380
362 IF K<10THENC=3:GOTO380
364 IF K<14 THENC=4:GOTO380
366 C=0
380 IF OC=-1 THEN 395
385 IF C=OC OR C=0 THEN 395
390 IF C<>0 THEN IF C<>OC AND XO=I#8 THEN C=OC
395 PLOT I,J,C:IF C<>0 THEN OC=C:XO=I#8
400 NEXT I:NEXT J
410 VOLUME 1,15:SOUND 1,4000,10
429 GPRINT "Press any key",0,23,15:A=INKEY(30)
430 A=INKEY(10):IF A=0 THEN 430
440 SCREEN1:END


----------------------------------------------------------------------------------------

10 REM *********************************************
20 REM **              MANDELBROT SET             **
30 REM **                                         **
40 REM **          MULTICOLOR 2 VERSION           **
50 REM *********************************************
60 REM ORIGINAL CODE BY JOSEPH DEWEY
70 REM LM80C BASIC PORT BY LEONARDO MILIANI
80 REM ** TO CHANGE THE SHAPE, CHANGE XL,XU,YL,YU.**
90 REM ** RE CHANGES THE RESOLUTION**
90 REM **TO CHANGE THE SHAPE, CHANGE**
91 REM **XL,XU,YL,YU. RE CHANGES  **
92 REM **THE RESOLUTION**
100 XL=-2.000:XU=0.500
110 YL=-1.100:YU=1.100
120 RE=20:WI=256:HE=192
130 XI=(XU-XL)/WI
140 YI=(YU-YL)/HE
150 RESTORE 450:DIM CL(10):FOR I=0 TO 9:READ CL(I):NEXT
160 A(10)=0
200 REM MAIN ROUTINE
210 SCREEN2:COLOR15,1,5
220 FOR J=0 TO HE-1:OC=-1
230 FOR I=0 TO WI-1
235 REM CALCULATE MANDELBROT
240 C=0
250 NR=XL+I*XI
260 NI=YL+J*YI
270 RZ=0:IZ=0
280 R2=0:I2=0
290 FOR K=1 TO RE
300 R2=RZ*RZ-IZ*IZ
310 I2=2*RZ*IZ
320 RZ=R2+NR
330 IZ=I2+NI
340 IF (RZ*RZ+IZ*IZ)>4 THEN 360
350 NEXT K
355 REM PLOT MANDELBROT
360 C=CL(K#2)
380 IF OC=-1 THEN 395
385 IF C=OC OR C=0 THEN 395
390 IF C<>0 THEN IF C<>OC AND XO=I#8 THEN C=OC
395 PLOT I,J,C:IF C<>0 THEN OC=C:XO=I#8
400 NEXT I:NEXT J
410 VOLUME 1,15:SOUND 1,4000,20
420 GPRINT "Press any key",0,23,15:A=INKEY(30)
430 A=INKEY(10):IF A=0 THEN 430
440 SCREEN1:END
450 DATA 4,5,7,12,2,3,10,11,9,6
